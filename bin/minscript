#!/usr/bin/env coffee
vm       = require 'vm'
fs       = require 'fs'
util     = require 'util'
readline = require 'readline'
path     = require 'path'
Module   = require 'module'

parser  = require '../lib/parser'
emitter = require '../lib/emitter'
wrapper = require '../lib/wrapper'
ctx     = vm.createContext()

last = (ary)-> ary[ary.length-1]

_compile = (code)->
  ast  = parser.parse(code)

  namespace = ast.filter((s)-> s[0] is 'ns')[0]
  throw "Missing namespace declaration" unless namespace

  cleanAst = ast.filter (s)-> s[0] isnt 'ns'
  gen = emitter.init("exports")
  wrapper.wrap namespace[1], gen.emit(cleanAst)

translate = (source)->
  code = fs.readFileSync(source).toString()
  _compile(code)

compile = (source)->
  js = translate(source)
  name = source.split('.')[0..-2].join(".")
  fs.writeFileSync("#{name}.js", js)

run = (ms)->
  js = translate(ms)
  console.log js

  mainModule = require.main
  mainModule.filename = process.argv[1] = '.'
  mainModule.moduleCache and= {}
  dir = fs.realpathSync '.'

  mainModule.paths = require('module')._nodeModulePaths dir
  mainModule._compile js, mainModule.filename

requireMS = (mod, source)->
  raw = fs.readFileSync source, 'utf8'
  js  = _compile(raw)
  mod._compile js, source

if require.extensions
  require.extensions[".ms"] = requireMS

repl = ->
  replEmitter = emitter.init()

  rl = readline.createInterface
    input: process.stdin
    output: process.stdout

  rl.setPrompt('MinScript > ')
  rl.prompt()

  buffer = ''

  rl.on 'line', (line)->
    buffer += "\n#{line}"

    open_count  = (buffer.match(/\(/g) or []).length
    close_count = (buffer.match(/\)/g) or []).length

    if open_count is close_count
      ast = parser.parse(buffer)
      console.log ast
      js = replEmitter.emit(ast)
      console.log js
      console.log "=>", vm.runInContext(js, ctx)
      buffer = ''
      rl.prompt()
    else
      rl.prompt()

switch process.argv[2]
  when 'repl'
    repl()
  when 'compile'
    compile(process.argv[3])
  else
    if process.argv[2]
      run(process.argv[2])
    else
      repl()
